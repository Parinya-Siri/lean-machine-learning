import data.zmod.basic
import data.nat.basic
import tactic.interval_cases
import group_theory.specific_groups.dihedral
import group_theory.specific_groups.cyclic
@[derive decidable_eq]
inductive quaternion_group (n : ℕ) : Type
| a : zmod (2 * n) → quaternion_group
| xa : zmod (2 * n) → quaternion_group
namespace quaternion_group
variables {n : ℕ}
private def mul : quaternion_group n → quaternion_group n → quaternion_group n
| (a i) (a j) := a (i + j)
| (a i) (xa j) := xa (j - i)
| (xa i) (a j) := xa (i + j)
| (xa i) (xa j) := a (n + j - i)
private def one : quaternion_group n := a 0
instance : inhabited (quaternion_group n) := ⟨one⟩
private def inv : quaternion_group n → quaternion_group n
| (a i) := a (-i)
| (xa i) := xa (n + i)
instance : group (quaternion_group n) :=
{ mul := mul,
  mul_assoc :=
    begin
      rintros (i | i) (j | j) (k | k);
      simp only [mul];
      abel,
      simp only [neg_mul, one_mul, int.cast_one, zsmul_eq_mul, int.cast_neg,
                 add_right_inj],
      calc -(n : zmod (2 * n)) = 0 - n : by rw zero_sub
        ... = 2 * n - n : by { norm_cast, simp, }
        ... = n : by ring
    end,
  one := one,
  one_mul :=
    begin
      rintros (i | i),
      { exact congr_arg a (zero_add i) },
      { exact congr_arg xa (sub_zero i) },
    end,
  mul_one := begin
      rintros (i | i),
      { exact congr_arg a (add_zero i) },
      { exact congr_arg xa (add_zero i) },
    end,
  inv := inv,
  mul_left_inv := begin
      rintros (i | i),
      { exact congr_arg a (neg_add_self i) },
      { exact congr_arg a (sub_self (n + i)) },
    end }
variable {n}
@[simp] lemma a_mul_a (i j : zmod (2 * n)) : a i * a j = a (i + j) := rfl
@[simp] lemma a_mul_xa (i j : zmod (2 * n)) : a i * xa j = xa (j - i) := rfl
@[simp] lemma xa_mul_a (i j : zmod (2 * n)) : xa i * a j = xa (i + j) := rfl
@[simp] lemma xa_mul_xa (i j : zmod (2 * n)) : xa i * xa j = a (n + j - i) := rfl
lemma one_def : (1 : quaternion_group n) = a 0 := rfl
private def fintype_helper : (zmod (2 * n) ⊕ zmod (2 * n)) ≃ quaternion_group n :=
{ inv_fun := λ i, match i with
                 | (a j) := sum.inl j
                 | (xa j) := sum.inr j
                 end,
  to_fun := λ i, match i with
                 | (sum.inl j) := a j
                 | (sum.inr j) := xa j
                 end,
  left_inv := by rintro (x | x); refl,
  right_inv := by rintro (x | x); refl }
def quaternion_group_zero_equiv_dihedral_group_zero : quaternion_group 0 ≃* dihedral_group 0 :=
{ to_fun := λ i, quaternion_group.rec_on i dihedral_group.r dihedral_group.sr,
  inv_fun := λ i, match i with
                | (dihedral_group.r j) := a j
                | (dihedral_group.sr j) := xa j
                end,
  left_inv := by rintro (k | k); refl,
  right_inv := by rintro (k | k); refl,
  map_mul' := by { rintros (k | k) (l | l); { dsimp, simp, }, } }
private lemma succ_mul_pos_fact {m : ℕ} [hn : fact (0 < n)] : fact (0 < (nat.succ m) * n) :=
⟨nat.succ_mul_pos m hn.1⟩
local attribute [instance] succ_mul_pos_fact
instance [fact (0 < n)] : fintype (quaternion_group n) := fintype.of_equiv _ fintype_helper
instance : nontrivial (quaternion_group n) := ⟨⟨a 0, xa 0, dec_trivial⟩⟩
lemma card [fact (0 < n)] : fintype.card (quaternion_group n) = 4 * n :=
begin
  rw [← fintype.card_eq.mpr ⟨fintype_helper⟩, fintype.card_sum, zmod.card, two_mul],
  ring
end
@[simp] lemma a_one_pow (k : ℕ) : (a 1 : quaternion_group n) ^ k = a k :=
begin
  induction k with k IH,
  { rw nat.cast_zero, refl },
  { rw [pow_succ, IH, a_mul_a],
    congr' 1,
    norm_cast,
    rw nat.one_add }
end
@[simp] lemma a_one_pow_n : (a 1 : quaternion_group n)^(2 * n) = 1 :=
begin
  rw [a_one_pow, one_def],
  congr' 1,
  exact zmod.nat_cast_self _
end
@[simp] lemma xa_sq (i : zmod (2 * n)) : xa i ^ 2 = a n :=
begin
  simp [sq]
end
@[simp] lemma xa_pow_four (i : zmod (2 * n)) : xa i ^ 4 = 1 :=
begin
  simp only [pow_succ, sq, xa_mul_xa, xa_mul_a, add_sub_cancel, add_sub_assoc, add_sub_cancel',
             sub_self, add_zero],
  norm_cast,
  rw ← two_mul,
  simp [one_def],
end
@[simp] lemma order_of_xa [hpos : fact (0 < n)] (i : zmod (2 * n)) : order_of (xa i) = 4 :=
begin
  change _ = 2^2,
  haveI : fact(nat.prime 2) := fact.mk (nat.prime_two),
  apply order_of_eq_prime_pow,
  { intro h,
    simp only [pow_one, xa_sq] at h,
    injection h with h',
    apply_fun zmod.val at h',
    apply_fun ( / n) at h',
    simp only [zmod.val_nat_cast, zmod.val_zero, nat.zero_div, nat.mod_mul_left_div_self,
             nat.div_self hpos.1] at h',
    norm_num at h' },
  { norm_num }
end
lemma quaternion_group_one_is_cyclic : is_cyclic (quaternion_group 1) :=
begin
  apply is_cyclic_of_order_of_eq_card,
  rw [card, mul_one],
  exact order_of_xa 0
end
@[simp] lemma order_of_a_one : order_of (a 1 : quaternion_group n) = 2 * n :=
begin
  rcases n.eq_zero_or_pos with rfl | hn,
  { simp_rw [mul_zero, order_of_eq_zero_iff'],
    intros n hn,
    rw [one_def, a_one_pow],
    apply mt a.inj,
    haveI : char_zero (zmod (2 * 0)) := zmod.char_zero,
    simpa using hn.ne' },
  haveI := fact.mk hn,
  apply (nat.le_of_dvd (nat.succ_mul_pos _ hn)
                       (order_of_dvd_of_pow_eq_one (@a_one_pow_n n))).lt_or_eq.resolve_left,
  intro h,
  have h1 : (a 1 : quaternion_group n)^(order_of (a 1)) = 1 := pow_order_of_eq_one _,
  rw a_one_pow at h1,
  injection h1 with h2,
  rw [← zmod.val_eq_zero, zmod.val_nat_cast, nat.mod_eq_of_lt h] at h2,
  exact absurd h2.symm (order_of_pos _).ne
end
lemma order_of_a [fact (0 < n)] (i : zmod (2 * n)) :
  order_of (a i) = (2 * n) / nat.gcd (2 * n) i.val :=
begin
  conv_lhs { rw ← zmod.nat_cast_zmod_val i },
  rw [← a_one_pow, order_of_pow, order_of_a_one]
end
lemma exponent : monoid.exponent (quaternion_group n) = 2 * lcm n 2 :=
begin
  rw [←normalize_eq 2, ←lcm_mul_left, normalize_eq],
  norm_num,
  rcases n.eq_zero_or_pos with rfl | hn,
  { simp only [lcm_zero_left, mul_zero],
    exact monoid.exponent_eq_zero_of_order_zero order_of_a_one },
  haveI := fact.mk hn,
  apply nat.dvd_antisymm,
  { apply monoid.exponent_dvd_of_forall_pow_eq_one,
    rintro (m | m),
    { rw [←order_of_dvd_iff_pow_eq_one, order_of_a],
      refine nat.dvd_trans ⟨gcd (2 * n) m.val, _⟩ (dvd_lcm_left (2 * n) 4),
      exact (nat.div_mul_cancel (nat.gcd_dvd_left (2 * n) (m.val))).symm },
    { rw [←order_of_dvd_iff_pow_eq_one, order_of_xa],
      exact dvd_lcm_right (2 * n) 4 } },
  { apply lcm_dvd,
    { convert monoid.order_dvd_exponent (a 1),
      exact order_of_a_one.symm },
    { convert monoid.order_dvd_exponent (xa 0),
      exact (order_of_xa 0).symm } }
end
end quaternion_group
