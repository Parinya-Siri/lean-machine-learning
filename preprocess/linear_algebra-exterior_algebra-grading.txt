import linear_algebra.exterior_algebra.basic
import ring_theory.graded_algebra.basic
namespace exterior_algebra
variables {R M : Type*} [comm_ring R] [add_comm_group M] [module R M]
variables (R M)
open_locale direct_sum
def graded_algebra.ι : M →ₗ[R] ⨁ i : ℕ, ↥((ι R : M →ₗ[_] _).range ^ i) :=
direct_sum.lof R ℕ (λ i, ↥((ι R : M →ₗ[_] _).range ^ i)) 1
  ∘ₗ (ι R).cod_restrict _ (λ m, by simpa only [pow_one] using linear_map.mem_range_self _ m)
lemma graded_algebra.ι_apply (m : M) :
  graded_algebra.ι R M m =
    direct_sum.of (λ i, ↥((ι R : M →ₗ[_] _).range ^ i)) 1
      (⟨ι R m, by simpa only [pow_one] using linear_map.mem_range_self _ m⟩) := rfl
lemma graded_algebra.ι_sq_zero (m : M) : graded_algebra.ι R M m * graded_algebra.ι R M m = 0 :=
begin
  rw [graded_algebra.ι_apply, direct_sum.of_mul_of],
  refine dfinsupp.single_eq_zero.mpr (subtype.ext $ ι_sq_zero _),
end
def graded_algebra.lift_ι : exterior_algebra R M →ₐ[R]
  ⨁ (i : ℕ), ↥((ι R).range ^ i : submodule R (exterior_algebra R M)) :=
lift R ⟨graded_algebra.ι R M, graded_algebra.ι_sq_zero R M⟩
variables {R M}
lemma graded_algebra.lift_ι_eq (i : ℕ)
  (x : ((ι R).range ^ i : submodule R (exterior_algebra R M))) :
  graded_algebra.lift_ι R M x =
    direct_sum.of (λ i, (((ι R).range ^ i : submodule R (exterior_algebra R M)) : Type*)) i x :=
begin
  cases x with x hx,
  dsimp only [subtype.coe_mk, direct_sum.lof_eq_of],
  refine submodule.pow_induction_on_left' _
    (λ r, _) (λ x y i hx hy ihx ihy, _) (λ m hm i x hx ih, _) hx,
  { rw [alg_hom.commutes, direct_sum.algebra_map_apply], refl },
  { rw [alg_hom.map_add, ihx, ihy, ←map_add], refl },
  { obtain ⟨_, rfl⟩ := hm,
    rw [alg_hom.map_mul, ih, graded_algebra.lift_ι, lift_ι_apply, graded_algebra.ι_apply,
      direct_sum.of_mul_of],
    exact direct_sum.of_eq_of_graded_monoid_eq (sigma.subtype_ext (add_comm _ _) rfl) }
end
instance graded_algebra :
  graded_algebra
    ((^) (ι R : M →ₗ[R] exterior_algebra R M).range : ℕ → submodule R (exterior_algebra R M)) :=
graded_algebra.of_alg_hom _ (graded_algebra.lift_ι R M)
