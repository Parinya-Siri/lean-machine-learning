import order.category.Lattice
import order.hom.complete_lattice
import topology.category.CompHaus
import topology.sets.opens
universes u
open category_theory opposite order topological_space
def Frame := bundled frame
namespace Frame
instance : has_coe_to_sort Frame Type* := bundled.has_coe_to_sort
instance (X : Frame) : frame X := X.str
def of (α : Type*) [frame α] : Frame := bundled.of α
@[simp] lemma coe_of (α : Type*) [frame α] : ↥(of α) = α := rfl
instance : inhabited Frame := ⟨of punit⟩
abbreviation hom (α β : Type*) [frame α] [frame β] : Type* := frame_hom α β
instance bundled_hom : bundled_hom hom :=
⟨λ α β [frame α] [frame β], by exactI (coe_fn : frame_hom α β → α → β),
 λ α [frame α], by exactI frame_hom.id α,
 λ α β γ [frame α] [frame β] [frame γ], by exactI frame_hom.comp,
 λ α β [frame α] [frame β], by exactI fun_like.coe_injective⟩
attribute [derive [large_category, concrete_category]] Frame
instance has_forget_to_Lattice : has_forget₂ Frame Lattice :=
{ forget₂ := { obj := λ X, ⟨X⟩, map := λ X Y, frame_hom.to_lattice_hom } }
@[simps] def iso.mk {α β : Frame.{u}} (e : α ≃o β) : α ≅ β :=
{ hom := e,
  inv := e.symm,
  hom_inv_id' := by { ext, exact e.symm_apply_apply _ },
  inv_hom_id' := by { ext, exact e.apply_symm_apply _ } }
end Frame
@[simps] def Top_op_to_Frame : Topᵒᵖ ⥤ Frame :=
{ obj := λ X, Frame.of (opens (unop X : Top)),
  map := λ X Y f, opens.comap $ quiver.hom.unop f,
  map_id' := λ X, opens.comap_id }
