import tactic.hint
namespace tactic
namespace auto_cases
meta structure auto_cases_tac :=
(name : string)
{α : Type}
(tac : expr → tactic α)
meta def tac_cases : auto_cases_tac := ⟨"cases", cases⟩
meta def tac_induction : auto_cases_tac := ⟨"induction", induction⟩
meta def find_tac : expr → option auto_cases_tac
| `(empty)     := tac_cases
| `(pempty)    := tac_cases
| `(false)     := tac_cases
| `(unit)      := tac_cases
| `(punit)     := tac_cases
| `(ulift _)   := tac_cases
| `(plift _)   := tac_cases
| `(prod _ _)  := tac_cases
| `(and _ _)   := tac_cases
| `(sigma _)   := tac_cases
| `(psigma _)  := tac_cases
| `(subtype _) := tac_cases
| `(Exists _)  := tac_cases
| `(fin 0)     := tac_cases
| `(eq _ _)    := tac_induction
| `(quot _)    := tac_induction
| _            := none
end auto_cases
meta def auto_cases_at (hyp : expr) : tactic string :=
do t ← infer_type hyp >>= whnf,
   match auto_cases.find_tac t with
   | some atac := do
     atac.tac hyp,
     pp ← pp hyp,
     return sformat!"{atac.name} {pp}"
   | none := fail "hypothesis type unsupported"
   end
@[hint_tactic]
meta def auto_cases : tactic string :=
do l ← local_context,
   results ← successes $ l.reverse.map auto_cases_at,
   when (results.empty) $
     fail "`auto_cases` did not find any hypotheses to apply `cases` or `induction` to",
   return (string.intercalate ", " results)
end tactic
