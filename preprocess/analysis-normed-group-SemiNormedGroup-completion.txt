import analysis.normed.group.SemiNormedGroup
import category_theory.preadditive.additive_functor
import analysis.normed.group.hom_completion
noncomputable theory
universe u
open uniform_space mul_opposite category_theory normed_group_hom
namespace SemiNormedGroup
@[simps]
def Completion : SemiNormedGroup.{u} ⥤ SemiNormedGroup.{u} :=
{ obj := λ V, SemiNormedGroup.of (completion V),
  map := λ V W f, f.completion,
  map_id' := λ V, completion_id,
  map_comp' := λ U V W f g, (completion_comp f g).symm }
instance Completion_complete_space {V : SemiNormedGroup} : complete_space (Completion.obj V) :=
completion.complete_space _
@[simps]
def Completion.incl {V : SemiNormedGroup} : V ⟶ Completion.obj V :=
{ to_fun := λ v, (v : completion V),
  map_add' := completion.coe_add,
  bound' := ⟨1, λ v, by simp⟩ }
lemma Completion.norm_incl_eq {V : SemiNormedGroup} {v : V} : ∥Completion.incl v∥ = ∥v∥ := by simp
lemma Completion.map_norm_noninc {V W : SemiNormedGroup} {f : V ⟶ W} (hf : f.norm_noninc) :
  (Completion.map f).norm_noninc :=
normed_group_hom.norm_noninc.norm_noninc_iff_norm_le_one.2 $
  (normed_group_hom.norm_completion f).le.trans $
  normed_group_hom.norm_noninc.norm_noninc_iff_norm_le_one.1 hf
def Completion.map_hom (V W : SemiNormedGroup.{u}) :
  (V ⟶ W) →+ (Completion.obj V ⟶ Completion.obj W) :=
add_monoid_hom.mk' (category_theory.functor.map Completion) $ λ f g,
  f.completion_add g
@[simp] lemma Completion.map_zero (V W : SemiNormedGroup) : Completion.map (0 : V ⟶ W) = 0 :=
(Completion.map_hom V W).map_zero
instance : preadditive SemiNormedGroup.{u} :=
{ hom_group := λ P Q, infer_instance,
  add_comp' := by { intros, ext,
    simp only [normed_group_hom.add_apply, category_theory.comp_apply, map_add] },
  comp_add' := by { intros, ext,
    simp only [normed_group_hom.add_apply, category_theory.comp_apply, map_add] } }
instance : functor.additive Completion :=
{ map_add' := λ X Y, (Completion.map_hom _ _).map_add }
def Completion.lift {V W : SemiNormedGroup} [complete_space W] [separated_space W] (f : V ⟶ W) :
  Completion.obj V ⟶ W :=
{ to_fun := f.extension,
  map_add' := f.extension.to_add_monoid_hom.map_add',
  bound' := f.extension.bound' }
lemma Completion.lift_comp_incl {V W : SemiNormedGroup} [complete_space W] [separated_space W]
  (f : V ⟶ W) : Completion.incl ≫ (Completion.lift f) = f :=
by { ext, apply normed_group_hom.extension_coe }
lemma Completion.lift_unique {V W : SemiNormedGroup} [complete_space W] [separated_space W]
  (f : V ⟶ W) (g : Completion.obj V ⟶ W) : Completion.incl ≫ g = f → g = Completion.lift f :=
λ h, (normed_group_hom.extension_unique _ (λ v, ((ext_iff.1 h) v).symm)).symm
end SemiNormedGroup
