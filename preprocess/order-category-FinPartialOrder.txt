import category_theory.Fintype
import order.category.PartialOrder
universes u v
open category_theory
structure FinPartialOrder :=
(to_PartialOrder : PartialOrder)
[is_fintype : fintype to_PartialOrder]
namespace FinPartialOrder
instance : has_coe_to_sort FinPartialOrder Type* := ⟨λ X, X.to_PartialOrder⟩
instance (X : FinPartialOrder) : partial_order X := X.to_PartialOrder.str
attribute [instance]  FinPartialOrder.is_fintype
@[simp] lemma coe_to_PartialOrder (X : FinPartialOrder) : ↥X.to_PartialOrder = ↥X := rfl
def of (α : Type*) [partial_order α] [fintype α] : FinPartialOrder := ⟨⟨α⟩⟩
@[simp] lemma coe_of (α : Type*) [partial_order α] [fintype α] : ↥(of α) = α := rfl
instance : inhabited FinPartialOrder := ⟨of punit⟩
instance large_category : large_category FinPartialOrder :=
induced_category.category FinPartialOrder.to_PartialOrder
instance concrete_category : concrete_category FinPartialOrder :=
induced_category.concrete_category FinPartialOrder.to_PartialOrder
instance has_forget_to_PartialOrder : has_forget₂ FinPartialOrder PartialOrder :=
induced_category.has_forget₂ FinPartialOrder.to_PartialOrder
instance has_forget_to_Fintype : has_forget₂ FinPartialOrder Fintype :=
{ forget₂ := { obj := λ X, ⟨X⟩, map := λ X Y, coe_fn } }
@[simps] def iso.mk {α β : FinPartialOrder.{u}} (e : α ≃o β) : α ≅ β :=
{ hom := e,
  inv := e.symm,
  hom_inv_id' := by { ext, exact e.symm_apply_apply _ },
  inv_hom_id' := by { ext, exact e.apply_symm_apply _ } }
@[simps] def dual : FinPartialOrder ⥤ FinPartialOrder :=
{ obj := λ X, of Xᵒᵈ, map := λ X Y, order_hom.dual }
@[simps functor inverse] def dual_equiv : FinPartialOrder ≌ FinPartialOrder :=
equivalence.mk dual dual
  (nat_iso.of_components (λ X, iso.mk $ order_iso.dual_dual X) $ λ X Y f, rfl)
  (nat_iso.of_components (λ X, iso.mk $ order_iso.dual_dual X) $ λ X Y f, rfl)
end FinPartialOrder
lemma FinPartialOrder_dual_comp_forget_to_PartialOrder :
  FinPartialOrder.dual ⋙ forget₂ FinPartialOrder PartialOrder =
    forget₂ FinPartialOrder PartialOrder ⋙ PartialOrder.dual := rfl
